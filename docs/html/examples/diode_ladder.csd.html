<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
 </head>
<body style="background-color:LightGrey;">
    <script src="CsoundAudioNode.js"></script>
    <script src="csound_loader.js"></script>
    <script>
    var handleMessage = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var onPlayClick = async function() {
        var csd = document.getElementById('csd').value;
        let csound = get_csound(handleMessage);
        if (csound == null) {
            return;
        }
        csound.CompileCsdText(csd);
        csound.Start();
        csound.Perform();
    }
    var onPlayStop = async function() {
        let csound = get_csound(handleMessage);
        csound.Stop();
    }
  </script>
<h1 style="font-family:sans-serif;">diode_ladder</h1>
<p>
This should play if your Web browser has WebAssembly enabled (most do). You can edit and replay the code. At this time, most examples will play in WebAssembly unless they need to load files. The first time you click <i>Play</i>, Csound will load. After that, clicking on <i>Play</i> will actually play.
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<p>
<textarea id="csd" style="width:98vw;height:45vh;font-family:monospace;background-color:#050570;color:#F0F090;"><CsoundSynthesizer>
<CsOptions>
-o dac
</CsOptions>
<CsInstruments>
sr =     48000 ; Changed for WebAssembly from: 44100
ksmps =  128 ; Changed for WebAssembly from: 16
nchnls = 2 ; Changed for WebAssembly from: 2
0dbfs=1


gi_sine ftgen 0, 0, 65537, 10, 1

gkcut init 6000


instr modulation 
  gkcut = lfo(4000, 0.1) + 6000 
endin

instr bass	 

  iamp = ampdbfs(-12) 
  ipch = cps2pch(p4, 12)

  asig = vco2(0.5, ipch, 0)

  acut = expon:a(i(gkcut), p3, 200) 
  aout = diode_ladder(asig, acut, 8, 1, 4)

  aout *= expseg:a(1.0, p3 - 0.05, 1.0, 0.05, 0.001) 

  aout = limit(aout, -1.0, 1.0)

  outc(aout, aout)

endin


gipat[] init 8
gipat[0] = 6.00
gipat[1] = 7.00
gipat[2] = 6.00
gipat[3] = 7.00
gipat[4] = 5.07
gipat[5] = 6.07
gipat[6] = 5.08
gipat[7] = 6.08


instr player
  indx = p4

  ;; play instrument
  if(gipat[indx] > 0) then
    schedule("bass", 0, 0.2, gipat[indx])
  endif

  ;; temporal recursion
  schedule("player", 0.2, 0.1, (indx + 1) % lenarray(gipat))

endin

schedule("modulation", 0, -1)
schedule("player", 0, 0.1, 0)
event_i("e", 0, 0.1 * 128)

</CsInstruments>

<CsScore>  
</CsScore>

</CsoundSynthesizer>
</textarea>
<textarea id="console" readonly style="width:98vw;height:40vh;font-family:monospace;background-color:DarkSlateGrey;color:LawnGreen;">
</textarea>
</body>
</html>