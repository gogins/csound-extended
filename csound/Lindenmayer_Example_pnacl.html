<!DOCTYPE html>
<html>
    <head>
        <title>Lindenmayer Example</title>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/dat-gui/0.5.1/dat.gui.js"></script>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/numeric/1.2.6/numeric.js"></script>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.1.0/jquery.js"></script>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/sprintf/1.0.3/sprintf.js"></script>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r73/three.js"></script>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/tinycolor/1.4.1/tinycolor.js"></script>
        <script type="text/javascript" src="js/Silencio.js"></script>
        <script type="text/javascript" src="js/ChordSpace.js"></script>
        <script type="text/javascript" src="js/ParametricLindenmayer.js"></script>
        <meta http-equiv="Cache-Control" content="no-cache, no-store, must-revalidate" />
        <meta http-equiv="Pragma" content="no-cache" />
        <meta http-equiv="Expires" content="0" />
        <script>
            try {
                var nw_window = nwgui.Window.get();
                nw_window.on('close', function() {
                    console.log('Closing down...');
                    //process.exit(0);
                }); 
            } catch (err) {
                console.log(err.message);
            }
        </script>
    </head>
    <body style="background:black;">
        <style>
                ::-webkit-scrollbar
            {
                width:10px;
            }
            ::-webkit-scrollbar-track-piece
            {
                background-coor:transparent;
            }
        </style>
        <table id = 'statistics' style="position: absolute; z-index:1; left:1vw;color:#eee;top:1vw;font-family:Monaco, sans-serif;font-size:8pt">
            <col>
            <col>
            <tr style="color:gold;font-size:24px;">
            <!-- Copyright (C) 2016 by Michael Gogins -->
            <td style="font-weight:bold;">Lindenmayer Example</td>
            <td style="color:gold;text-align:right;">Michael Gogins</td>
            </tr>
            <tr style="font-size:24px;">
            <td>Time:</td>
            <td id="Time_cell" style="text-align:right;color:LawnGreen;font-weight:bold;">0</td>
            </tr>
            <tr>
            <!-- or: http://stackoverflow.com/questions/35509360/how-to-make-the-content-of-the-following-table-cell-scrollable -->
            <td colspan=2><textarea id="console" style="color:SkyBlue;background-color:transparent;border:none;text-align:left;font-size:10px;height:200px;width:550px;overflow:auto;"></textarea></td>
            </tr>
    </table>
     <canvas id = 'graphics_canvas' class='canvas' style="position: absolute; top:0px; left:0px; width:98vw;height:98vh;" /canvas>
    <textarea class="code" id="orc" hidden rows=24 cols=80>
opcode hertz2midinn, i, i
ihertz xin
print ihertz
; m = 12*log2(fm/440 Hz) + 69
ilog2 = log(2)
imidinn = 12 * (log(ihertz / 440) / ilog2) + 69
print imidinn
xout imidinn
endop

; Each Csound instrument uses global variables
; and function tables declared in the global area of the orchestra
; just above the instrument definition itself. A naming pattern is
; used to avoid name collisions and keep everything straight.

gaSendM,gaSendL,gaSendR init 0
gi_FM_Clang_sine ftgen 0,0,4096,10,1 ;A SINE WAVE. USED BY THE LFOs.
gi_FM_Clang_detuning ftgen 0,0,128,21,1,1 ; random array used for fixing unique detune values for each note
gi_FM_Clang_DryMap ftgen 0,0,4096,7,1,2048,1,2048,0 ; dry mixer control mapping
gi_FM_Clang_WetMap ftgen 0,0,4096,7,0,2048,1,2048,1 ; wet mixer control mapping
gaSendL,gaSendR init 0 ; initialise variable used for sending audio between instruments
gi_FM_Clang_Imp ftgen 0,0,4097,9,0.5,1,0 ; shape for the hammer inpulse
gi_FM_Clang_stereo ftgen 0,0,256,21,1,1 ; random array used for fixing unique stereo values for each note
gi_FM_Clang_detune ftgen 0,0,256,21,1,1 ; random array used for fixing unique detune values for each note
gi_FM_Clang_scale ftgen 0,0,128,-7,-1,128,1

instr FM_Clang_controls ; Read in widgets
iporttime = 0.05
gk_FM_Clang_mod chnget "mod"
gk_FM_Clang_mod port gk_FM_Clang_mod,iporttime
gk_FM_Clang_NdxVel chnget "NdxVel"
gk_FM_Clang_Dur chnget "Dur"
gk_FM_Clang_FiltFund chnget "FiltFund"
gk_FM_Clang_NdxCurve chnget "NdxCurve"
gk_FM_Clang_LPF chnget "LPF"
gk_FM_Clang_ModDep chnget "ModDep"
gk_FM_Clang_ModRte chnget "ModRte"
gk_FM_Clang_ModRteKyb chnget "ModRteKyb"
gk_FM_Clang_ModMix chnget "ModMix"
gk_FM_Clang_Sustain chnget "Sustain"
gk_FM_Clang_CarKyb chnget "CarKyb"
gk_FM_Clang_StWidth chnget "StWidth"
gk_FM_Clang_StWidth init 0.01
gk_FM_Clang_StMix chnget "StMix"
gk_FM_Clang_DryWet chnget "RvbDryWet"
gk_FM_Clang_Dry table gk_FM_Clang_DryWet,gi_FM_Clang_DryMap,1 ; map dry/wet control
gk_FM_Clang_Wet table gk_FM_Clang_DryWet,gi_FM_Clang_WetMap,1 ;
gk_FM_Clang_RvbSize chnget "RvbSize"
gk_FM_Clang_Detune chnget "Detune"
gk_FM_Clang_DtnMix chnget "DtnMix"
gk_FM_Clang_Amp chnget "Amp"
gk_FM_Clang_level chnget "gk_FM_Clang_level"
gk_FM_Clang_Amp port gk_FM_Clang_Amp,iporttime
gk_FM_Clang_AttTim chnget "AttTim"
gk_FM_Clang_AttVel chnget "AttVel"
gk_FM_Clang_NseAmp chnget "NseAmp"
gk_FM_Clang_NseBW chnget "NseBW"
gk_FM_Clang_NseFllw chnget "NseFllw"
endin

gi_Droner_overlap init .1
gk_Droner_distort_factor init 0.4
gk_Droner_first_harmonic init 0.4
gk_Droner_slider1 init .05
gk_Droner_slider2 init .1
gk_Droner_slider3 init .04
gk_Droner_slider4 init .2
gk_Droner_slider5 init .02
gk_Droner_level init 0
gi_Droner_sine ftgen 0, 0, 65536, 10, 1, 0, .02
instr Droner
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
k1 init .5
k2 init .05
k3 init .1
k4 init .2
k5 init .1
k6 init .05
k7 init .1
k8 init 0
k9 init 0
k10 init 0
k3 = gk_Droner_slider1
k4 = gk_Droner_slider2
k5 = gk_Droner_slider3
k6 = gk_Droner_slider4
k7 = gk_Droner_slider5
kwaveform init 0
iamp = ampdb(ivelocity)
iattack = gi_Droner_overlap
idecay = gi_Droner_overlap
isustain = p3
p3 = iattack + isustain + idecay
aenvelope transegr 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
if kwaveform == 0 then
asignal poscil3 1, ihertz, gi_Droner_sine
endif
if kwaveform == 1 then
asignal vco2 1, ihertz, 8 ; integrated saw
endif
if kwaveform == 2 then
asignal vco2 1, ihertz, 12 ; triangle
endif
asignal chebyshevpoly asignal, 0, k1, k2, k3, k4, k5, k6, k7, k8, k9, k10
asignal = asignal * aenvelope * 10
aleft, aright pan2 asignal, ipan
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
kgain = ampdb(gk_Droner_level)
aleft = adamping * aleft * kgain
aright = adamping * aright * kgain
; Each instrument sends its audio to a global audio output bus,
; where effects can be applied.
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

opcode FM_Clang_veloc, i, iii
; *p->r = *p->ilo + csound->curip->m_veloc*(*p->ihi - *p->ilo) * dv127;
ivel, ilow, ihigh xin
xout ilow + ivel * (ihigh - ilow) * 0.007874
endop

instr FM_Clang ; Sound producing instrument. Triggered by MIDI notes or score notes.
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
icps = cpsmidinn(ikey)
inum = ikey
iamplitude = ampdb(ivelocity)
; icps cpsmidi
; inum notnum
iKybdScl table inum,gi_FM_Clang_scale ; Keyboard scaling value. Note '0' maps to a value of -1, note 64 maps to a value of zero and note 127 maps to a value of 1.
; Amplitude Envelope
idur = i(gk_FM_Clang_Dur)
iAmpCurve = -8
iRelTim = (i(gk_FM_Clang_Sustain)==1?idur:0.1)
;iAttVel veloc i(gk_FM_Clang_AttVel)*0.05,0
iAttVel FM_Clang_veloc ivelocity, i(gk_FM_Clang_AttVel)*0.05,0
iAttTim = iAttVel + i(gk_FM_Clang_AttTim)
if iAttTim==0 then
aAEnv transegr 1,idur,iAmpCurve,0, iRelTim,iAmpCurve,0
else
aAEnv transegr 0,iAttTim,iAmpCurve, 1,idur,iAmpCurve,0, iRelTim,iAmpCurve,0
endif
; Key velocity to amplitude
;iAVel veloc 0,0.1
iAVel FM_Clang_veloc ivelocity, 0, 0.1
; FM index
iNdx veloc 0,i(gk_FM_Clang_NdxVel) ; Key velocity to FM index
iNdx FM_Clang_veloc ivelocity, 0,i(gk_FM_Clang_NdxVel) ; Key velocity to FM index
kIEnv transeg iNdx*octave(-iKybdScl*4),idur,-i(gk_FM_Clang_NdxCurve),0 ; Index envelope. Amplitude influenced by 'Index' widget control and keyboard scaling (index reduced for higher notes, increased for lower notes)
; Carrier frequency keyboard scaling
icar = 1 * octave(iKybdScl*i(gk_FM_Clang_CarKyb))
; Carrier ratio modulation
iModRte table inum,gi_FM_Clang_scale
iModRte = octave(iModRte*i(gk_FM_Clang_ModRteKyb))
kDepEnv transeg 1,idur,-4,0
amod lfo gk_FM_Clang_ModDep*0.1*kDepEnv, gk_FM_Clang_ModRte * iModRte, 0
; Noise bandpass filter cutoff as it respond to the 'Carrier Follow' switch
kcf = gk_FM_Clang_NseFllw==1?icps*gk_FM_Clang_mod:icps
; Noise amplitude keyboard following
kNseAmp = octave(iKybdScl*-4)*gk_FM_Clang_NseAmp
; Main FM algorithm
if gk_FM_Clang_NseAmp>0 then ; If noise amplitude is greater than zero...
aNse gauss kNseAmp ; ... generate some gaussian noise
aNse butbp aNse,kcf,kcf*gk_FM_Clang_NseBW ; bandpass filter the noise
aNse butbp aNse,kcf,kcf*gk_FM_Clang_NseBW ; and again
else
aNse = 0 ; Otherwise no noise signal!
endif
idetune table inum,gi_FM_Clang_detune ; Read a detuning value from a random table
asig foscil iAVel, icps*cent(-gk_FM_Clang_Detune*idetune), icar +aNse, gk_FM_Clang_mod, kIEnv, gi_FM_Clang_sine ; FM pair
aModSig foscil iAVel, icps*cent(-gk_FM_Clang_Detune*idetune), icar+amod+aNse, gk_FM_Clang_mod, kIEnv, gi_FM_Clang_sine ; FM pair with modulation
asig = asig + (aModSig * gk_FM_Clang_ModMix) ; Mix the two FM pairs.
; Auxilliary FM algorithm (used for detuning)
if gk_FM_Clang_DtnMix>0&&gk_FM_Clang_Detune>0 then
if gk_FM_Clang_NseAmp>0 then
aNse gauss kNseAmp
aNse butbp aNse,kcf,kcf*gk_FM_Clang_NseBW
aNse butbp aNse,kcf,kcf*gk_FM_Clang_NseBW
else
aNse = 0
endif
idetune table inum+128,gi_FM_Clang_detune ; Read a detuning value from a random table
asig2 foscil iAVel, icps*cent(gk_FM_Clang_Detune*idetune), icar +aNse, gk_FM_Clang_mod, kIEnv, gi_FM_Clang_sine ; Detuning applied to fundemental
aModSig2 foscil iAVel, icps*cent(gk_FM_Clang_Detune*idetune), icar+amod+aNse, gk_FM_Clang_mod, kIEnv, gi_FM_Clang_sine
asig2 = asig2 + (aModSig2 * gk_FM_Clang_ModMix)
asig = asig + asig2*gk_FM_Clang_DtnMix
endif
; Filter Fundemental
if i(gk_FM_Clang_FiltFund)==1 then ; If filter fundemental switch is active
asig butbr asig,icps*icar,icps*icar*0.3 ; Filter out fundemental
asig butbr asig,icps*icar,icps*icar*0.3 ; and again to make sure it is all gone
endif
; Lowpass Filter
if i(gk_FM_Clang_LPF)<32 then ; If lowpass filter is less than maximum...
kCF limit icps*gk_FM_Clang_LPF,20,20000 ; ...create the cutoff value in CPS based on fundemental and LPF ratio control. Limit to prevent out of range values.
asig clfilt asig,kCF,0,2 ; Butterworth lowpass filter
endif
; Amplitude Envelope
kgain = ampdb(gk_FM_Clang_level)
asig = asig * aAEnv * gk_FM_Clang_Amp * kgain; Apply amplitude envelope
; Stereo Effect
iDlyTimL table inum,gi_FM_Clang_stereo ; Read a delay time value from a random table based on note played
iDlyTimR table inum+128,gi_FM_Clang_stereo ; Read a different delay time value from a random table based on note played
if gk_FM_Clang_StWidth>0&&gk_FM_Clang_StMix>0 then ; If 'width' is above zero and 'mix' is above zero...
aL vdelay asig, iDlyTimL*gk_FM_Clang_StWidth*1000, 0.2*1000 ; ...left channel delay
aR vdelay asig, iDlyTimR*gk_FM_Clang_StWidth*1000, 0.2*1000 ; right channel delay
aL ntrpol asig, aL, gk_FM_Clang_StMix ; Mix delayed and dry signal
aR ntrpol asig, aR, gk_FM_Clang_StMix ;
aleft = aL;*gk_FM_Clang_Dry
aright = aR;*gk_FM_Clang_Dry
gaSendL = gaSendL + aL ; Send to reverb
gaSendR = gaSendR + aR
else
aleft = asig;*gk_FM_Clang_Dry
aright = asig;*gk_FM_Clang_Dry
gaSendL = gaSendL + asig ; Send to reverb
gaSendR = gaSendR + asig
endif
aleft = aleft * ampdb(ivelocity)
aright = aleft * ampdb(ivelocity)
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gk_Harpsichord_level init 0
gk_HarpsichordPan init .5
gi_Harptable ftgen 0, 0, 65536, 7, -1, 1024, 1, 1024, -1
instr Harpsichord
//////////////////////////////////////////////
// Original by James Kelley.
// Adapted by Michael Gogins.
//////////////////////////////////////////////
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
iattack = .005
isustain = p3
irelease = .3
p3 = iattack + isustain + irelease
iHz = cpsmidinn(ikey)
kHz = k(iHz)
iamplitude = ampdb(ivelocity)
aenvelope 	 transeg 1.0, 20.0, -10.0, 0.05
apluck 	 pluck 1, kHz, iHz, 0, 1
aharp 	 poscil 1, kHz, gi_Harptable
aharp2 	 balance apluck, aharp
asignal			 = (apluck + aharp2) * iamplitude * aenvelope * 10
adeclick linsegr 0, iattack, 1, isustain, 1, irelease, 0
kgain = ampdb(gk_Harpsichord_level)
asignal = asignal * adeclick * kgain
aoutleft, aoutright pan2 asignal, ipan
chnmix aoutleft, "out_left"
chnmix aoutright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Sweeper_overlap init .1
gk_Sweeper_britel init 0
gk_Sweeper_briteh init 2.9
gk_Sweeper_britels init .2 / 3
gk_Sweeper_britehs init 2.5 / 2
gk_Sweeper_level init 0
gi_Sweeper_sine ftgen 0, 0, 65536, 10, 1
gi_Sweeper_octfn ftgen 0, 0, 65536, -19, 1, 0.5, 270, 0.5
instr Sweeper
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
iamp = ampdb(ivelocity)
iattack = gi_Sweeper_overlap
idecay = gi_Sweeper_overlap
isustain = p3
p3 = iattack + isustain + idecay
aenvelope transegr 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
icps = ihertz
kamp expseg 0.001,0.02,0.2,p3-0.01,0.001
ktonemoddep jspline 0.01,0.05,0.2
ktonemodrte jspline 6,0.1,0.2
ktone poscil3 ktonemoddep, ktonemodrte, gi_Sweeper_sine
kbrite rspline gk_Sweeper_britel, gk_Sweeper_briteh, gk_Sweeper_britels, gk_Sweeper_britehs
ibasfreq init icps
ioctcnt init 3
iphs init 0
a1 hsboscil 1, ktone, kbrite, ibasfreq, gi_Sweeper_sine, gi_Sweeper_octfn, ioctcnt, iphs
a1 = a1 * aenvelope
amod poscil3 0.25, ibasfreq*(1/3), gi_Sweeper_sine
arm = a1*amod
kmix expseg 0.001, 0.01, rnd(1), rnd(3)+0.3, 0.001
kmix=.25
a1 ntrpol a1, arm, kmix
;a1 pareq a1/10, 400, 15, .707
;a1 tone a1, 500
kpanrte jspline 5, 0.05, 0.1
kpandep jspline 0.9, 0.2, 0.4
kpan poscil3 kpandep, kpanrte, gi_Sweeper_sine
a1,a2 pan2 a1, kpan
a1 delay a1, rnd(0.1)
a2 delay a2, rnd(0.11)
aenv linsegr 1, 1, 0
aenv = aenvelope
aleft = a1*aenv*.02
aright = a2*aenv*.02
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
kgain = ampdb(gk_Sweeper_level)
aleft = adamping * aleft * kgain
aright = adamping * aright * kgain
chnmix aleft, "out_left"
chnmix aright, "out_right"
; printks "gk_Sweeper_level: %f kgain: %f aleft: %f\n", 0.5, gk_Sweeper_level, kgain, k(aleft)
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gi_Buzzer_overlap init .1
gk_Buzzer_harmonics init 2
gk_Buzzer_level init 0
gk_Buzzer_distort_factor init 0.4
gk_Buzzer_first_harmonic init 0.4
gi_Buzzer_sine ftgen 0, 0, 65536, 10, 1
instr Buzzer
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
iamp = ampdb(ivelocity) * 4
iattack = gi_Buzzer_overlap
idecay = gi_Buzzer_overlap
isustain = p3
p3 = iattack + isustain + idecay
aenvelope transegr 0.0, iattack / 2.0, 1.5, iamp / 2.0, iattack / 2.0, -1.5, iamp, isustain, 0.0, iamp, idecay / 2.0, 1.5, iamp / 2.0, idecay / 2.0, -1.5, 0
ihertz = cpsmidinn(ikey)
asignal buzz aenvelope, ihertz, gk_Buzzer_harmonics, gi_Buzzer_sine
asignal = asignal * 3
aleft, aright pan2 asignal, ipan
adamping linseg 0, 0.03, 1, p3 - 0.1, 1, 0.07, 0
kgain = ampdb(gk_Buzzer_level)
aleft = adamping * aleft * kgain
aright = adamping * aright * kgain
chnmix aleft, "out_left"
chnmix aright, "out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

; All control channels come into this always-on instrument so that
; the channel need be read only once per kperiod for greater efficiency.

instr Controls
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
gk_Droner_slider1 chnget "gk_Droner_slider1"
gk_Droner_slider2 chnget "gk_Droner_slider2"
gk_Droner_slider3 chnget "gk_Droner_slider3"
gk_Droner_slider4 chnget "gk_Droner_slider4"
gk_Droner_slider5 chnget "gk_Droner_slider5"
gk_Droner_level chnget "gk_Droner_level"
gk_Buzzer_harmonics chnget "gk_Buzzer_harmonics"
gk_Buzzer_level chnget "gk_Buzzer_level"
gk_Sweeper_level chnget "gk_Sweeper_level"
gk_Harpsichord_level chnget "gk_Harpsichord_level"
gk_Reverb_Feedback chnget "gk_Reverb_Feedback"
gk_MasterOutput_level chnget "gk_MasterOutput_level"
prints "instr %4d t %12.4f d %12.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gk_FM_Clang_preset init 1
instr FM_Clang_preset
gk_FM_Clang_preset chnget "gk_FM_Clang_preset"
ktrig changed gk_FM_Clang_preset
#define SET_PRESET(N'Amp'AttTim'AttVel'Dur'Sustain'mod'NdxVel'NdxCurve'CarKyb'DtnMix'Detune'LPF'FiltFund'NseAmp'NseBW'NseFllw'ModMix'ModDep'ModRte'ModRteKyb'StMix'StWidth'RvbDryWet'RvbSize)
#
if i(gk_FM_Clang_preset)==$N then
printks2 "setting preset %d\n", $N
chnset $Amp ,"Amp"
chnset $AttTim ,"AttTim"
chnset $AttVel ,"AttVel"
chnset $Dur ,"Dur"
chnset $Sustain ,"Sustain"
chnset $mod ,"mod"
chnset $NdxVel ,"NdxVel"
chnset $NdxCurve ,"NdxCurve"
chnset $CarKyb ,"CarKyb"
chnset $DtnMix ,"DtnMix"
chnset $Detune ,"Detune"
chnset $LPF ,"LPF"
chnset $FiltFund ,"FiltFund"
chnset $NseAmp ,"NseAmp"
chnset $NseBW ,"NseBW"
chnset $NseFllw ,"NseFllw"
chnset $ModMix ,"ModMix"
chnset $ModDep ,"ModDep"
chnset $ModRte ,"ModRte"
chnset $ModRteKyb,"ModRteKyb"
chnset $StMix ,"StMix"
chnset $StWidth ,"StWidth"
chnset $RvbDryWet,"RvbDryWet"
chnset $RvbSize ,"RvbSize"
endif
#
if ktrig==1 then
reinit SEND_PRESET
endif
SEND_PRESET:
; N'Amp'AttTim'AttVel'Dur'Sustain'mod 'NdxVel'NdxCurve'CarKyb'DtnMix'Detune'LPF'FiltFund'NseAmp'NseBW'NseFllw'ModMix'ModDep'ModRte'ModRteKyb'StMix'StWidth'RvbDryWet'RvbSize)
$SET_PRESET(1'0.5'0 '0 '12 '1 '2.29'1 '16 '0 '1 '1.5 '16 '1 '1000 '0.001'0 '1 '0.1 '3 '0 '0.5 '0.01 '0.3 '0.55 )
$SET_PRESET(2'0.8'0 '0 '1.7'1 '5.04'0.55 '100 '0 '0 '0 '3.8'0 '0 '0.001'0 '0 '0.1 '3 '0 '0.5 '0.01 '0.1 '0.55 )
$SET_PRESET(3'0.5'0.1 '0.5 '3.8'1 '1.00'2 '28 '0 '1 '15 '16 '0 '500 '0.0077'1 '1 '0.1 '3 '0 '0.5 '0.01 '0.3 '0.55 )
$SET_PRESET(4'0.5'0 '0.006 '24 '1 '3.19'1 '15 '0 '0.47 '2.5 '16 '1 '0 '0.001'0 '1 '0.1 '0.33 '1.7 '0.5 '0.01 '0.3 '0.55 )
$SET_PRESET(5'0.8'0 '1 '6.6'0 '1.12'1 '13 '-2.68 '1 '25 '3.8'0 '0 '0.001'0 '0 '0.1 '3 '0 '1 '0.02 '0.172 '0.21 )
$SET_PRESET(6'3.25'0.1 '1 '1.28'0 '2.55'0.55 '35 '-2.3 '0 '1.5 '16 '1 '2000 '0.0025'0 '0 '0.1 '3 '0 '0.5 '0.01 '0.68 '0.93 )
$SET_PRESET(7'4 '0.1 '1 '2.39'1 '2.55'0.55 '35 '-2.3 '0 '1.5 '16 '1 '16 '0.1262'0 '0 '0.1 '3 '0 '0 '0.01 '1.00 '0 )
$SET_PRESET(8'1 '0 '0 '14.25'1 '6.97'0.55 '35 '-2.3 '0.13 '1.5 '4.78'1 '16 '0.0326'0 '1 '0.1 '3 '0 '0 '0.01 '0.6 '0 )
$SET_PRESET(9'1.65'0 '0 '1.7'1 '1.20'0.55 '100 '0 '0 '0 '3.8'0 '0 '0.001'0 '0 '0.1 '3 '0 '0.5 '0.01 '0.1 '0.55 )
$SET_PRESET(10'2' 0 '0 '1 '1 '2.15'0.55 '100 '0 '0.21 '12 '3.8'0 '0 '0.001'0 '0 '0.1 '3 '0 '0.5 '0.01 '0.35 '0.37 )
$SET_PRESET(11'2 '0.1 '1 '3.44'1 '3.75'0.55 '100 '0 '0.21 '25 '16 '0 '0 '0.001'1 '0 '0.1 '3 '0 '0.5 '0.05 '0 '0.37 )
$SET_PRESET(12'2 '0.1 '1 '1.45'1 '2.66'1.00 '16 '0 '0.21 '25 '16 '0 '0 '0.001'1 '0 '0.1 '3 '0 '0.5 '0.05 '0.6 '0.37 )
$SET_PRESET(13'1.9'0 '0 '24 '1 '2.29'1.00 '23.25 '-4 '1 '1.5 '16 '1 '151 '0.0178'1 '1 '0.1 '3 '0 '0.5 '0.01 '0.3 '0.55 )
endin

; The global effects receive audio from the global audio output bus,
; and send it on to their own global output busses.

gk_Reverb_Feedback init 0.975
gk_Reverb_DelayModulation init 0.875
instr Reverb
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
ain_left chnget "out_left"
ain_right chnget "out_right"
azero init 0
aout_left init 0
aout_left, aright reverbsc ain_left, azero, gk_Reverb_Feedback, 15000.0
aleft init 0
azero init 0
aleft, aout_right reverbsc azero, ain_right, gk_Reverb_Feedback, 15000.0
chnmix aout_left, "reverb_out_left"
chnmix aout_right, "reverb_out_right"
prints "instr %4d t %9.4f d %9.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

gk_MasterOutput_level init 0
instr MasterOutput
; Consume unused pfields.
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
ain_left chnget "reverb_out_left"
ain_right chnget "reverb_out_right"
kgain = ampdb(gk_MasterOutput_level)
aout_left = ain_left * gk_MasterOutput_level * kgain * 0.00000001
aout_right = ain_right * gk_MasterOutput_level * kgain * 0.00000001
outs aout_left, aout_right
prints "instr %4d t %12.4f d %12.4f k %9.4f v %9.4f p %9.4f\n", p1, p2, p3, p4, p5, p7
endin

; This instrument clears the global output busses of audio for the next kperiod.

instr Clear
insno = p1
itime = p2
iduration = p3
ikey = p4
ivelocity = p5
iphase = p7
ipan = p6
idepth = p8
iheight = p9
ipcs = p10
ihomogeneity = p11
chnclear "out_left"
chnclear "out_right"
chnclear "reverb_out_left"
chnclear "reverb_out_right"
prints "instr %4d t %12.4f d %12.4f\n", p1, p2, p3
endin

; Finally the orchestra itself turns on the effects instruments and the
; other always-on instruments.

scoreline_i "i \"Controls\" 0 -1"
scoreline_i "i \"FM_Clang_controls\" 0 -1"
scoreline_i "i \"FM_Clang_preset\" 0 -1"
scoreline_i "i \"Reverb\" 0 -1"
scoreline_i "i \"MasterOutput\" 0 -1"
scoreline_i "i \"Clear\" 0 -1"
    </textarea>
    <script type="text/javascript">
        window.onerror = function (message, url, line) {
          csound.message(message + '\n' + url + '\n' + line + '\n');
          console.trace();
        }
        var title = 'Lindenmayer  Example';
        var context = null;
        var lsys = new ParametricLindenmayer.PLSystem();
        var startTime = 0;
        var generate = function() {
          csound.message("generate()...\n");
          try {
            var startTime = 0;
            var canvas = document.getElementById('graphics_canvas');
            canvas.width = window.innerWidth;
            canvas.height = window.innerHeight;
            var ctx = canvas.getContext("2d");
            ctx.clearRect(0, 0, canvas.width, canvas.height);
            var H = canvas.height;
            var W = canvas.width;
            // Move(i,t,d,k,v,p)
            lsys.set_axiom('T(0);Push();Move(-1,5,0,-4,0,0);AA();Pop();Push();Move(0,5,0,4,0,0);AA();Pop();Move(1,5,0,-4,0,0);A();');
            lsys.add_rule('A();',  'iteration < 3',                 'Note();Move(1,5,0,4,0,0);A();Push();T(2);Move(-11,5,0,4,0,0);AA();Pop();Move(1,5,0,4,0,0);Note();Move(1,5,0,4,0,0);Note();Move(1,5,0,4,0,0);A();Note();');
            lsys.add_rule('A();',  'iteration == 3',                'T(5);A();Note();Move(1,5,0,4,0,0;Note();Move(0,2,0,-1,0,0);Push();Move(-21,5,0,-4,0,0);AA();Pop();K();Note();Move(1,5,0,4,0,0);A();Move(1,5,0,4,0,0);Note();');
            lsys.add_rule('AA();', 'iteration == 3',                'qqNote();Move(-10,-5,0,-14,0,0);Scale(0, .95);A();Scale(0, 1/.975);Push();Move(1,5,0,-14,0,0);K();AA();Pop();Move(-1,5,0,4,0,0);qqNote();Note();Move(1,5,0,4,0,0);Note();Move(1,5,0,4,0,0);AA();qqNote();');
            lsys.add_rule('AA();', 'iteration > 3 && iteration < 6',  'Note();Move(-1,5,0,4,0,0);Scale(0, .95);A();Scale(0, 1/.975);Push();Move(1,5,0,4,0,0);AA();Pop();Move(-1,5,0,4,0,0);qqNote();Note();Move(-1,5,0,4,0,0);Note();Move(1,5,0,-4,0,0);AA();Note();');
            lsys.add_rule('AA();', 'iteration <  3',                'AA();Note();Move(1,-5,0,4,0,0);Note();Move(1,5,0,-14,0,0);Push();Move(1,-5,0,4,0,0);AA();Push();Move(-1,5,0,-4,0,0);AA();Pop();Pop();qqNote();Move(1,5,0,4,0,0);Q(2);AA();Move(1,5,0,-4,0,0);Note();');
            var chord = ChordSpace.chordForName('Fm');
            console.log(chord.information());
            var t = new ParametricLindenmayer.Turtle(new Silencio.Event(), chord, chord);
            t.note.duration = 1;
            lsys.set_turtle(t);
            lsys.generate(5);
            lsys.score.findScales();
            csound.message('lsys:' + lsys + '\n');
            csound.message(lsys.score.getDuration().toString() + '\n');
            lsys.score.sort();
            lsys.score.setScale(1, 1, 5);
            lsys.score.setScale(3, 2, 9.9);
            //lsys.score.setScale(3, 5, 0);
            lsys.score.setScale(4, 24, 72);
            lsys.score.temper(12);
            lsys.score.setScale(5, 60, 10);
            lsys.score.setScale(6, .4, .6);
            lsys.score.setScale(7, .4, .6);
            lsys.score.tieOverlaps(true);
            lsys.conformToChords();
            lsys.score.setDuration(530);
            csound.message('Cleaning up some annoying things in the score...');
            for (var i = 0; i < lsys.score.data.length; i++) {
                var event = lsys.score.data[i];
                // We didn't generate pan, so just randomize it.
                var pan = Math.random() * 2 - 1;
                event.pan = pan;
                var minimum_gap = null;
                var minimum_gap_event = null;
                // Move too-high notes for "Harpsichord" down.
                if (Math.floor(event.channel) === 3) {
                    while (event.key > 67) {
                        event.key = event.key - 12;
            }
                }
                // Close up gaps in the score.
                for (j = i + 1; j < lsys.score.data.length - 1; j++) {
                    var gap_event = lsys.score.data[j];
                    var gap = gap_event.time - event.end;
                    if (gap <= 0) {
                        minimum_gap = 0;
                        minimum_gap_event = null;
                        break;
                    }
                    if (minimum_gap == null) {
                        minimum_gap = gap;
                        minimum_gap_event = gap_event;
                    }
                    if (gap < minimum_gap) {
                        minimum_gap = gap;
                    }
                }
                if (minimum_gap_event !== null && minimum_gap > 0) {
                    event.end = minimum_gap_event.time;
                }
            }
            // Pianoteq misbehaves if we start right at time 0.
            for (var i = 0; i < lsys.score.data.length; i++) {
                var event = lsys.score.data[i];
                event.time = event.time + 1;
            }
            // Also if keys overlap.
            for (var i = 0; i < lsys.score.data.length; i++) {
                var event = lsys.score.data[i];
                console.log(event.toIStatement() + ' e: ' + event.end);
                var channel = Math.floor(event.channel);
                var key = event.key;
                for (var j = i + 1; j < lsys.score.data.length; j++) {
                    var overlap = lsys.score.data[j];
                    var overlap_channel = Math.floor(overlap.channel);
                    var overlap_key = overlap.key;
                    if (key === overlap_key && channel === overlap_channel && channel === 2) {
                        if (event.end > overlap.time) {
                            var the_end = Math.max(event.end, overlap.end);
                            console.log('Correcting overlap for:');
                            console.log('  ' + event.toIStatement() + ' e: ' + event.end);
                            console.log('  ' + overlap.toIStatement() + ' e: ' + overlap.end);
                            event.end = the_end;
                            overlap.end = the_end;
                            console.log('to:');
                            console.log('  ' + event.toIStatement() + ' e: ' + event.end);
                            console.log('  ' + overlap.toIStatement() + ' e: ' + overlap.end + '\n');
                         }
                    }
                }
             }
            var panskip = .05;
            var leftpan = panskip / 2;
            var panrange = 1 - panskip;
            lsys.score.setScale(6, leftpan, panrange);
            lsys.score.setScale(7, leftpan, panrange);
            lsys.score.findScales();
            csound.message(lsys.score.minima.toString());
            csound.message(lsys.score.ranges.toString());
            csound.message('Generated ' + lsys.score.data.length + ' notes.\n');
            context = lsys.score.draw(canvas, W, H);
            lsys.score.add(540, 1, 144, 999);
            lsys.score.sendToCsound(csound);
           } catch(err) {
            csound.message(err.name + ': ' + err.message + ' line: ' + err.line + '\n');
            csound.message(err.stack + '\n');
            console.log(err.stack);
          }
        }
        function csoundMessage(text) {
            var Log_cell = document.getElementById('Log_cell');
            Log_cell.value += text;
            console.log(text);
            Log_cell.scrollTop = Log_cell.scrollHeight;
         }
        function onWindowResize( event ) {
            var container = document.getElementById('graphics_canvas');
        }
        function handleMessage(message) {
            csoundMessage(message);
        }
        function updateTimeCell() {
            var score_time = csound.getScoreTime();
            var score_time_cell = document.getElementById('Time_cell');
            score_time_cell.innerHTML = score_time.toFixed(4);
            setTimeout(updateTimeCell, .02);
            lsys.score.progress(score_time);
        };
        var parameters = {
            gk_Bower_pressure: 0.25,
            gk_Bower_level: 0.5,
            gk_Blower_grainDensity: .150,
            gk_Blower_grainDuration: .2,
            gk_Blower_grainAmplitudeRange: .1,
            gk_Blower_grainFrequencyRange: .033,
            gk_Blower_level: 0.5,
            gk_Buzzer_Harmonics: 15,
            gk_Buzzer_level: 0.5,
            gk_Droner_partial1: 0.1,
            gk_Droner_partial2: 0.1,
            gk_Droner_partial3: 0.1,
            gk_Droner_partial4: 0.1,
            gk_Droner_partial5: 0.1,
            gk_Droner_level: 0.5,
            gk_Harpsichord_level: 0.5,
            gk_Harpsichord_pick: .75,
            gk_Harpsichord_reflection: .5,
            gk_Harpsichord_pluck: .75,
            gk_Phaser_ratio1: 1,
            gk_Phaser_ratio2: .3333334,
            gk_Phaser_index1: 1,
            gk_Phaser_index2: .0125,
            gk_Phaser_level: 0.5,
            gk_Piano_level: 0.5,
            gk_Shiner_level: 0.5,
            gk_Sweeper_britel: 0,
            gk_Sweeper_briteh: 2.9,
            gk_Sweeper_britels: .2 / 3,
            gk_Sweeper_britehs: 2.5 / 2,
            gk_Sweeper_level: 0.5,
            gk_YiString_reverb_send: 0.5,
            gk_YiString_chorus_send: 0.5,
            gk_YiString_level: 0.5,
            gk_SolinaChorus_chorus_lfo1_hz: 18,
            gk_SolinaChorus_chorus_lfo1_amp: .6,
            gk_SolinaChorus_chorus_lfo2_hz: 6,
            gk_SolinaChorus_chorus_lfo2_amp: .2,
            gk_Reverb_Feedback: .975,
            gk_Reverb2_Feedback: .7,
            gk_MasterOutput_level: .4,
            gk_overlap: 0.05,
            gk_FM_Clang_preset: 1,
            gk_FM_Clang_level: 0,
             Play_Stop: function() {
                if (typeof csound.is_playing === 'undefined') {
                    csound.is_playing = false;
                }
                if (csound.is_playing === false) {
                    csound.perform();
                    gui.revert();
                    csound.compileOrc(document.getElementById('orc').value);
                    generate();
                    start_time_seconds = new Date().getTime() / 1000.0;
                    csound.is_playing = true;
                } else {
                    csound.is_playing = false;
                    csound.stop();
                }
            },
            Pause_Resume: function() {
                if (csound.is_paused === false) {
                    csound.pause();
                    csound.is_paused = true;
                } else {
                    csound.resume();
                    csound.is_paused = false;
                }
            },
        };
        var gui = null;
        var parameters_default_json = {"preset":"Default","remembered":{"Default":{"0":{"gk_overlap":7.966035720428351,"gk_Reverb_Feedback":0.7647639458262565,"gk_Reverb2_Feedback":0.7,"gk_MasterOutput_level":0.4,"gk_FM_Clang_preset":8,"gk_FM_Clang_level":8.488855828305333,"gk_Blower_grainDensity":122.66065388951522,"gk_Blower_grainDuration":0.2479543763947432,"gk_Blower_grainAmplitudeRange":79.36865839909808,"gk_Blower_grainFrequencyRange":25.25366403607666,"gk_Blower_level":0.5,"gk_Bower_pressure":0.4316242848352937,"gk_Bower_level":8.488855828305333,"gk_Buzzer_Harmonics":4.775417619454314,"gk_Buzzer_level":2.6114187582077193,"gk_Droner_partial1":0.2846883580828533,"gk_Droner_partial2":0.47754176194543135,"gk_Droner_partial3":0.1,"gk_Droner_partial4":0.1,"gk_Droner_partial5":0.3581563214590735,"gk_Droner_level":1.8767391244455212,"gk_Harpsichord_pick":0.75,"gk_Harpsichord_reflection":0.6042838951521984,"gk_Harpsichord_pluck":0.75,"gk_Harpsichord_level":9.223535462067538,"gk_Phaser_ratio1":1.6234498308906427,"gk_Phaser_ratio2":0.7936865839909808,"gk_Phaser_index1":1,"gk_Phaser_index2":0.43291995490417134,"gk_Phaser_level":4.815457659494328,"gk_Piano_level":0.5,"gk_Shiner_level":2.6114187582077193,"gk_Sweeper_britel":1.432625285836294,"gk_Sweeper_briteh":2.9,"gk_Sweeper_britels":1.2856893590838536,"gk_Sweeper_britehs":2.5713787181677072,"gk_Sweeper_level":0.5,"gk_YiString_reverb_send":0.5,"gk_YiString_chorus_send":0.5,"gk_YiString_level":10.692894729591941}}},"closed":false,"folders":{"Master effects":{"preset":"Default","closed":false,"folders":{}},"Csound instruments":{"preset":"Default","closed":false,"folders":{"FM Clang":{"preset":"Default","closed":false,"folders":{}},"Blower":{"preset":"Default","closed":false,"folders":{}},"Bower":{"preset":"Default","closed":false,"folders":{}},"Buzzer":{"preset":"Default","closed":false,"folders":{}},"Droner":{"preset":"Default","closed":false,"folders":{}},"Harpsichord":{"preset":"Default","closed":false,"folders":{}},"Phaser":{"preset":"Default","closed":false,"folders":{}},"Pianoteq":{"preset":"Default","closed":true,"folders":{}},"Shiner":{"preset":"Default","closed":false,"folders":{}},"Sweeper":{"preset":"Default","closed":false,"folders":{}},"YiString":{"preset":"Default","closed":false,"folders":{}}}}}};
        window.onload = function() {
            csound.message('platform: ' + navigator.platform + '\n');
            var parameters_json = Silencio.restoreDatGuiJson(parameters_default_json);
            gui = new dat.GUI({load: parameters_json, width: 300});
            gui.remember(parameters);
            gui.add(parameters, 'Play_Stop').name('Play/Stop');
            var Master = gui.addFolder('Master effects');
            add_slider(Master, 'gk_overlap', 0, 20);
            add_slider(Master, 'gk_Reverb_Feedback', 0, 1);
            add_slider(Master, 'gk_Reverb2_Feedback', 0, 1);
            add_slider(Master, 'gk_MasterOutput_level', -40, 40);
            var intruments = gui.addFolder('Csound instruments');
            var fmclang = intruments.addFolder('FM Clang');
            add_slider(fmclang, 'gk_FM_Clang_preset', 1, 13);
            add_slider(fmclang, 'gk_FM_Clang_level', -40, 40);
            var Blower = intruments.addFolder('Blower');
            add_slider(Blower, 'gk_Blower_grainDensity', 0, 400);
            add_slider(Blower, 'gk_Blower_grainDuration', 0, .5);
            add_slider(Blower, 'gk_Blower_grainAmplitudeRange', 0, 400);
            add_slider(Blower, 'gk_Blower_grainFrequencyRange', 0, 100);
            add_slider(Blower, 'gk_Blower_level', -40, 40);
            var Bower = intruments.addFolder('Bower');
            add_slider(Bower, 'gk_Bower_pressure', 0, 1);
            add_slider(Bower, 'gk_Bower_level', -40, 40);
            var Buzzer = intruments.addFolder('Buzzer');
            add_slider(Buzzer, 'gk_Buzzer_Harmonics', 0, 20);
            add_slider(Buzzer, 'gk_Buzzer_level', -40, 40);
            var Droner = intruments.addFolder('Droner');
            add_slider(Droner, 'gk_Droner_partial1', 0, 1);
            add_slider(Droner, 'gk_Droner_partial2', 0, 1);
            add_slider(Droner, 'gk_Droner_partial3', 0, 1);
            add_slider(Droner, 'gk_Droner_partial4', 0, 1);
            add_slider(Droner, 'gk_Droner_partial5', 0, 1);
            add_slider(Droner, 'gk_Droner_level', -40, 40);
            var Harpsichord = intruments.addFolder('Harpsichord');
            add_slider(Harpsichord, 'gk_Harpsichord_pick', 0, 1);
            add_slider(Harpsichord, 'gk_Harpsichord_reflection', .000001, .999999);
            add_slider(Harpsichord, 'gk_Harpsichord_pluck', 0, 1);
            add_slider(Harpsichord, 'gk_Harpsichord_level', -40, 40);
            var Phaser = intruments.addFolder('Phaser');
            add_slider(Phaser, 'gk_Phaser_ratio1', 0, 2);
            add_slider(Phaser, 'gk_Phaser_ratio2', 0, 2);
            add_slider(Phaser, 'gk_Phaser_index1', 0, 1);
            add_slider(Phaser, 'gk_Phaser_index2', 0, 1);
            add_slider(Phaser, 'gk_Phaser_level', -40, 40);
            var Pianoteq = intruments.addFolder('Pianoteq');
            add_slider(Pianoteq, 'gk_Piano_level', -40, 40);
            var Shiner = intruments.addFolder('Shiner');
            add_slider(Shiner, 'gk_Shiner_level', -40, 40);
            var Sweeper = intruments.addFolder('Sweeper');
            add_slider(Sweeper, 'gk_Sweeper_britel', 0, 4);
            add_slider(Sweeper, 'gk_Sweeper_briteh', 0, 4);
            add_slider(Sweeper, 'gk_Sweeper_britels', 0, 4);
            add_slider(Sweeper, 'gk_Sweeper_britehs', 0, 4);
            add_slider(Sweeper, 'gk_Sweeper_level', -40, 40);
            var YiString = intruments.addFolder('YiString');
            add_slider(YiString, 'gk_YiString_reverb_send', 0, 1);
            add_slider(YiString, 'gk_YiString_chorus_send', 0, 1);
            add_slider(YiString, 'gk_YiString_level', -40, 40);
            gui.revert();
            parameters.Play_Stop();
        }
        function gk_update(name, value) {
            if (typeof csound !== 'undefined') {            
                var numberValue = parseFloat(value);
                csound.setControlChannel(name, numberValue);
                csound.message(name + ': ' + numberValue + '\n');
            }
        }
        function add_slider(gui_folder, token, minimum, maximum) {
            var on_parameter_change = function(value) {
                if (token.startsWith('gk_')) {
                    gk_update(token, value);
                }
            };
            var slider = gui_folder.add(parameters, token, minimum, maximum);
            slider.onChange(on_parameter_change);
            return slider;
        };
        if (typeof csound !== 'undefined') {
            csound.is_playing = false;
        }
    </script>
    <script type="text/javascript" src="csound.js"></script>
    <script>
    // Messages both from the "csound.js" wrapper and from Csound itself are combined in the 
    // "console" text area.
    function handleMessage(message) {
        console.log(message);
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message.data;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;    
    }
    </script>
    <!--The PNaCl csound module must be the very last element in the body, 
        because the "csound.js" script attaches the Csound module and its 
        callbacks to that element. -->
    <div id="engine"></div>
    </body>
</html>
